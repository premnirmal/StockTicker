import org.apache.tools.ant.taskdefs.condition.Os

apply from: "../build.gradle"
apply plugin: "com.android.application"
apply plugin: "dagger.hilt.android.plugin"
apply plugin: "kotlin-android"
apply plugin: "kotlin-parcelize"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "kotlin-kapt"

buildscript {
  repositories {
    mavenCentral()
    google()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
  }
  dependencies {
    classpath "com.google.gms:google-services:4.4.2"
    classpath "com.google.firebase:firebase-crashlytics-gradle:2.9.9"

    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

repositories {
  mavenCentral()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  maven { url "https://jitpack.io" }
  maven { url "https://maven.google.com" }
}

def getVersionName = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--tags', '--abbrev=0'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

def getOldGitVersion = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
      commandLine 'powershell', '-command', "& {git tag --sort=-committerdate | Select-Object -first 10 | Select-Object -last 1}"
    } else {
      commandLine 'sh', '-c', "git tag --sort=-committerdate | head -10 | tail -1"
    }
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

android {
  buildFeatures {
    buildConfig = true
  }

  namespace 'com.github.premnirmal.tickerwidget'
  compileSdk 35
  buildToolsVersion = "31.0.0"

  def final name = getVersionName()
  def final major = name.split("\\.")[0].toInteger()
  def final minor = name.split("\\.")[1].toInteger()
  def final patch = name.split("\\.")[2].toInteger()
  def final code = (major * 100000000) + (minor * 100000) + patch
  def final oldGitVersion = getOldGitVersion()
  println "Old git version $oldGitVersion"
  def final appIdBase = "com.github.premnirmal.tickerwidget"

  defaultConfig {
    applicationId appIdBase
    minSdkVersion 26
    targetSdkVersion 34

    versionCode = code
    versionName = name

    buildConfigField "String", "PREVIOUS_VERSION", "\"$oldGitVersion\""

    javaCompileOptions {
      annotationProcessorOptions {
        arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }

    kapt {
      arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
      }
    }
  }

  sourceSets {
    main.java.srcDirs += "src/main/kotlin"
    debug.java.srcDirs += "src/debug/kotlin"
    release.java.srcDirs += "src/release/kotlin"
    dev.java.srcDirs += "src/dev/kotlin"
    prod.java.srcDirs += "src/prod/kotlin"
    purefoss.java.srcDirs += "src/purefoss/kotlin"
    String testDir = "src/test/kotlin"
    test {
      resources.srcDirs += ["src/test/resources"]
      java.srcDir testDir
    }
  }

  dexOptions {
    javaMaxHeapSize "2048M"
  }

  buildFeatures {
    viewBinding = true
  }

  signingConfigs {
    release {
      storeFile file("file:keystore.jks")

      final File propsFile = file("file:keystore.properties")
      if (propsFile.exists()) {
        final Properties props = new Properties()
        props.load(new FileInputStream(propsFile))
        storePassword props.getProperty("key.store.password")
        keyPassword props.getProperty("key.alias.password")
        keyAlias props.getProperty("key.alias.alias")
      }
    }
  }

  flavorDimensions "mobile"

  productFlavors {
    dev {
      dimension "mobile"
      applicationId = appIdBase + ".dev"
    }
    prod {
      dimension "mobile"
      applicationId = appIdBase
    }
    purefoss {
      dimension "mobile"
      // no analytics, but still using the production packageName
      applicationId = appIdBase
    }
  }

  bundle {
    density {
      enableSplit true
    }
    abi {
      enableSplit true
    }
    language {
      enableSplit false
    }
  }

  buildTypes {
    release {
      debuggable false
      signingConfig signingConfigs.release
      minifyEnabled true
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
    debug {
      debuggable true
      ext.enableCrashlytics = false
      minifyEnabled true
      firebaseCrashlytics {
        mappingFileUploadEnabled false
      }
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }

  packagingOptions {
    resources {
      excludes +=
          ['META-INF/DEPENDENCIES', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_11.toString()
  }
  // Allow references to generated code
  kapt {
    correctErrorTypes true
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation "androidx.core:core-ktx:1.15.0"

  implementation "androidx.appcompat:appcompat:1.7.0"
  implementation "androidx.legacy:legacy-support-v4:1.0.0"
  implementation "com.google.android.material:material:1.12.0"
  implementation "androidx.preference:preference-ktx:1.2.1"
  implementation "androidx.browser:browser:1.8.0"
  implementation "androidx.constraintlayout:constraintlayout:2.2.0"
  implementation "androidx.viewpager2:viewpager2:1.1.0"
  implementation 'androidx.core:core-splashscreen:1.0.1'
  implementation "androidx.fragment:fragment-ktx:1.8.5"

  implementation "io.coil-kt:coil:2.2.2"

  implementation "javax.inject:javax.inject:1"
  implementation "javax.annotation:javax.annotation-api:1.3.2"

  implementation "com.google.dagger:hilt-android:2.48.1"
  kapt "com.google.dagger:hilt-compiler:2.48.1"

  implementation "com.squareup.okhttp3:okhttp:4.10.0"
  implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"
  implementation "com.squareup.retrofit2:retrofit:2.9.0"
  implementation "com.squareup.retrofit2:converter-gson:2.9.0"
  implementation "com.squareup.retrofit2:converter-simplexml:2.9.0"
  implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
  implementation "org.jsoup:jsoup:1.14.2"

  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
  implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.7"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7"
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.7"
  implementation "androidx.lifecycle:lifecycle-common-java8:2.8.7"
  implementation "androidx.work:work-runtime:2.10.0"
  implementation "androidx.work:work-runtime-ktx:2.10.0"

  implementation "com.jakewharton.threetenabp:threetenabp:1.4.6"

  implementation "com.jakewharton.timber:timber:4.7.1"
  implementation "com.github.PhilJay:MPAndroidChart:v3.0.3"

  implementation "androidx.room:room-runtime:2.6.1"
  kapt "androidx.room:room-compiler:2.6.1"
  implementation "androidx.room:room-ktx:2.6.1"

  prodImplementation 'com.google.android.play:review:2.0.2'
  prodImplementation 'com.google.android.play:review-ktx:2.0.2'

  implementation "com.robinhood.ticker:ticker:2.0.2"

  prodImplementation "com.google.firebase:firebase-crashlytics:19.3.0"
  prodImplementation "com.google.firebase:firebase-analytics:22.1.2"

//  debugImplementation "com.squareup.leakcanary:leakcanary-android:2.4"

  testImplementation 'com.google.dagger:hilt-android-testing:2.48.1'
  kaptTest 'com.google.dagger:hilt-compiler:2.48.1'

  testImplementation "junit:junit:4.13.2"
  testImplementation "org.assertj:assertj-core:3.8.0"
  testImplementation "org.robolectric:robolectric:4.10.3"
  testImplementation "androidx.test:runner:1.6.2"
  testImplementation "androidx.test:rules:1.6.1"
  testImplementation "androidx.annotation:annotation:1.9.1"
  testImplementation "androidx.test:rules:1.6.1"
  testImplementation "org.threeten:threetenbp:1.6.8"
  testImplementation "org.mockito:mockito-core:5.3.1"
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3"
  testImplementation "androidx.room:room-testing:2.6.1"

  // Need this to fix a class not found error in tests (https://github.com/robolectric/robolectric/issues/1932)
  testImplementation "org.khronos:opengl-api:gl1.1-android-2.1_r1"
}

configurations.configureEach {
  resolutionStrategy.force "org.objenesis:objenesis:2.5.1"
  exclude group: "xpp3", module: "xpp3"
}

android.applicationVariants.configureEach { variant ->
  if (!variant.name.toLowerCase().contains("prod")) {
    def googleTask = tasks.findByName("process${variant.name.capitalize()}GoogleServices")
    def crashlyticsMappingTask = tasks.
        findByName("uploadCrashlyticsMappingFile${variant.name.capitalize()}")
    println("flavour: ${variant.name}")
    println("disabling ${googleTask.name}")
    googleTask.enabled = false
    if (crashlyticsMappingTask != null) {
      println("disabling ${crashlyticsMappingTask.name}")
      crashlyticsMappingTask.enabled = false
    }
  }
}

